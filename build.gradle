import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "java"
    id "java-library"
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'com.github.johnrengelman.shadow' version '6.0.0'
}

def name = 'PureTickets'
group 'co.uk.magmo.pureTickets'
version '2.5.0'

repositories {
    jcenter()

    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    maven { url 'https://repo.spongepowered.org/maven' }
    maven { url "https://oss.sonatype.org/content/repositories/public/" }
    maven { url "https://repo.aikar.co/content/groups/aikar/" }
    maven { url "http://nexus.okkero.com/repository/maven-releases/" }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation 'com.github.kittinunf.fuel:fuel:2.2.3'
    
    api "co.aikar:acf-paper:0.5.0-SNAPSHOT"
    api "co.aikar:idb-core:1.0.0-SNAPSHOT"
    api "com.okkero.skedule:skedule:1.2.6"
    api "com.zaxxer:HikariCP:2.7.9"

    compileOnly "org.spigotmc:spigot-api:1.16.1-R0.1-SNAPSHOT"
}

compileJava {
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}

shadowJar {
    String base = "co.uk.magmo.puretickets.lib."

    relocate "kotlin", base + "kotlin"

    relocate "com.github.kittinunf.fuel", base + "fuel"
    relocate "com.github.kittinunf.result", base + "result"

    relocate "co.aikar.commands", base + "commands"
    relocate "co.aikar.locales", base + "locales"
    relocate 'co.aikar.idb', base + "idb"

    relocate 'com.zaxxer.hikari', base + "hikari"
    relocate 'com.okkero.skedule', base + "skedule"
    relocate "net.jodah.expiringmap", base + "expiringmap"

    archiveFileName = name + ".jar"
    mergeServiceFiles()
    minimize()
}

build {
    dependsOn(shadowJar)
}

if (file("$rootDir/local.gradle").exists()) {
    apply from: "$rootDir/local.gradle"
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        javaParameters = true
    }
}